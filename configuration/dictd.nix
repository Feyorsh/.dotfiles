{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.dictd;
in
{

  options = {
    services.dictd = {
      enable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = ''
          Whether to enable the DICT.org dictionary server.
        '';
      };

      DBs = lib.mkOption {
        type = lib.types.listOf lib.types.package;
        default = with pkgs.dictdDBs; [
          wiktionary
          wordnet
        ];
        defaultText = lib.literalExpression "with pkgs.dictdDBs; [ wiktionary wordnet ]";
        example = lib.literalExpression "[ pkgs.dictdDBs.nld2eng ]";
        description = "List of databases to make available.";
      };
    };
  };

  config =
    let
      dictdb = pkgs.dictDBCollector {
        dictlist = map (x: {
          name = x.name;
          filename = x;
        }) cfg.DBs;
      };
    in
      lib.mkIf cfg.enable {
        environment.systemPackages = [ pkgs.dict ];

        environment.etc."dict.conf".text = ''
          server localhost
        '';

        users.users.dictd = {
          description = "DICT.org dictd server";
          home = "${dictdb}/share/dictd";
          uid = 105;
        };
        users.knownUsers = [ "dictd" ];
        users.groups.dictd = {
          members = [ "dictd" ];
          gid = 105;
        };
        users.knownGroups = [ "dictd" ];

        launchd.daemons.dictd.serviceConfig = {
          ProgramArguments = [
            "${pkgs.dict}/sbin/dictd"
            "-s"
            "-c"
            "${dictdb}/share/dictd/dictd.conf"
            "--locale"
            "en_US.UTF-8"
          ];
          GroupName = "dictd";
          UserName = "dictd";
          RunAtLoad = true;
        };
      };
}
